plugins {
    id "idea"
    id "java"
    id "application"
}

ext {
    lombokVersion = "1.18.34"
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compileOnly "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"
}

project.configurations.implementation.setCanBeResolved(true)

abstract class ApplicationRunner extends JavaExec {
    ApplicationRunner() {
        description = "Application runner"
        group = "application"

        classpath = project.sourceSets.main.runtimeClasspath
        standardInput = System.in
        jvmArgs = ["-Xms32m", "-Xmx32m",
                   "-Dcom.sun.management.jmxremote",
                   "-Dcom.sun.management.jmxremote.port=9091",
                   "-Dcom.sun.management.jmxremote.authenticate=false",
                   "-Dcom.sun.management.jmxremote.ssl=false "]
    }
}

tasks.register("runEqualsAndHashCodeExample", ApplicationRunner) {
    mainClass = "ru.romanow.memory.leaks.EqualsAndHashCodeExample"
}

tasks.register("runUnclosedResourcesExample", ApplicationRunner) {
    mainClass = "ru.romanow.memory.leaks.UnclosedResourcesExample"
}

jar {
    enabled = false
}
